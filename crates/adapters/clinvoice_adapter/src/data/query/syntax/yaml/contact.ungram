/// A query for a `Contact`.
YamlContactQuery =
  Tab* 'address:literal_newline'
  YamlLocationQuery 'literal_newline'
  Tab* 'email:literal_newline'
  YamlContactQueryEmail
  Tab* 'phone:literal_newline'
  YamlContactQueryPhone

/// A query for the `email` field of a `Contact`.
YamlContactQueryEmail =
  MatchAny
| MatchCondition 'EqualTo' 'literal_newline'
  MatchValue Email
| MatchCondition 'HasAll' 'literal_newline'
  MatchValue YamlEmailList
| MatchCondition 'HasAny' 'literal_newline'
  MatchValue YamlEmailList
| MatchCondition 'HasNone' 'literal_newline'
  MatchValue YamlEmailList
| MatchCondition 'InRange' 'literal_newline'
  MatchValue min:YamlEmailListItem max:YamlEmailListItem

/// A list of `Email` addresses.
YamlEmailList =
  '[]'
| YamlEmailListItem YamlEmailListItem*

/// An item in a list of `Email` addresses.
YamlEmailListItem = 'literal_newline' OneOrMoreTab '- ' Email

/// A valid email address NOTE: definition may not comprise all valid email addresses, only most.
///
/// Example: foo@bar.io
Email = '"' EmailPrefix '@' EmailDomain '.top_level_domain' '"'

/// The part of an email before the '@'.
EmailPrefix =
  AlphabetLowercase EmailPrefix*
| Integer EmailPrefix*
| (AlphabetLowercase|Integer) ('-'|'_'|'.') (AlphabetLowercase|Integer) EmailPrefix*

/// The part of an email after the '@'.
EmailDomain = (AlphabetLowercase|Integer) (AlphabetLowercase|Integer|'-')* (AlphabetLowercase|Integer)

/// A query for the `phone` field of a `Contact`.
YamlContactQueryPhone =
  MatchAny
| MatchCondition 'EqualTo' 'literal_newline'
  MatchValue Phone
| MatchCondition 'HasAll' 'literal_newline'
  MatchValue YamlPhoneList
| MatchCondition 'HasAny' 'literal_newline'
  MatchValue YamlPhoneList
| MatchCondition 'HasNone' 'literal_newline'
  MatchValue YamlPhoneList
| MatchCondition 'InRange' 'literal_newline'
  MatchValue min:YamlPhoneListItem max:YamlPhoneListItem

/// A list of `Phone` numbers.
YamlPhoneList =
  '[]'
| YamlPhoneListItem YamlPhoneListItem*

/// An item in a list of `Phone` numbers.
YamlPhoneListItem = 'literal_newline' OneOrMoreTab '- ' Phone

/// A standard phone number. Contains an optional country code (e.g. 1-555-627-5309 or 555-627-5309).
Phone = '"' (Integer Integer* '-')? Integer Integer Integer '-' Integer Integer Integer '-' Integer Integer Integer Integer '"'
