//#include ../fields
//#include ../types

/// A query for the `DateTime` type.
YamlDateTimeQuery =
  MatchAny
| MatchCondition 'EqualTo' 'literal_newline'
  MatchValue DateTime
| MatchCondition 'HasAll' 'literal_newline'
  MatchValue YamlDateTimeList
| MatchCondition 'HasAny' 'literal_newline'
  MatchValue YamlDateTimeList
| MatchCondition 'HasNone' 'literal_newline'
  MatchValue YamlDateTimeList
| MatchCondition 'InRange' 'literal_newline'
  MatchValue min:YamlDateTimeListItem max:YamlDateTimeListItem

YamlDateTimeList =
  '[]'
| YamlDateTimeListItem YamlDateTimeListItem*

YamlDateTimeListItem = 'literal_newline' YamlFieldPrefix '- ' DateTime

/// A combination of `Date` and `Time`. The fields are separated by a literal 'T'.
DateTime = '"' Date 'T' Time '"'

/// A combination of `Year`, `Month`, and `Day` (e.g. 2000-01-01 for January 1st, 2000)
Date = Year '-' Month '-' Day

/// A combination of `Hour`, `Minute`, and `Second` (e.g. 21:30:00 for 9:30PM).
///
/// Also includes an offset to allow accounting for different timezones (e.g. -04:00 for EST)
Time = Hour ':' Minute ':' Second '-' offset_hour:Hour ':' offset_minute:Minute

/// A year (e.g. 2000)
Year = Integer Integer*

/// A month of the `Year`, where the number equates to which month it is (e.g. 12 for 'December')
Month =
  '0' Integer
| '1' ('0'|'1'|'2')

/// A day of the `Month`. Valid number of days depend on which `Month` it is.
Day =
  ('0'|'1'|'2') Integer
| '3' ('0'|'1')

/// An hour of the `Day`. AM/PM is encoded in 24-hour time (e.g. 14 for 2PM)
Hour =
  ('0'|'1') Integer
| '2' ('0'|'1'|'2'|'3')

/// A minute of the `Hour`. Cannot go over 60â€” that would be the next `Hour`.
Minute = ('0'|'1'|'2'|'3'|'4'|'5') Integer

/// Seconds of a `Minute`. Has the same formatting as `Minute`.
Second = Minute

/// A query for the `OptionalDateTime` type.
YamlOptionalDateTimeQuery =
  MatchAny
| MatchCondition 'EqualTo' 'literal_newline'
  MatchValue OptionalDateTime
| MatchCondition 'HasAll' 'literal_newline'
  MatchValue YamlOptionalDateTimeList
| MatchCondition 'HasAny' 'literal_newline'
  MatchValue YamlOptionalDateTimeList
| MatchCondition 'HasNone' 'literal_newline'
  MatchValue YamlOptionalDateTimeList
| MatchCondition 'InRange' 'literal_newline'
  MatchValue min:YamlOptionalDateTimeListItem max:YamlOptionalDateTimeListItem

YamlOptionalDateTimeList =
  '[]'
| YamlOptionalDateTimeListItem YamlOptionalDateTimeListItem*

YamlOptionalDateTimeListItem = 'literal_newline' YamlFieldPrefix '- ' OptionalDateTime

OptionalDateTime =
  '~'
| DateTime
